%import common.NEWLINE -> _NL

start      : entry+
entry      : _ACRONYM [protein_id] description+ [commentary _WS] [ref_id] _NL

commentary : "(" _seperated{_content, _COMMENTARY_SEP} ")"
_content    : protein_id description+ ref_id

!description: TOKEN+
    | [_WS] "(" TOKEN+ ")" ["-" | ", " | _WS]  // e.g. (S)-xxx or (1 g/kg ip), parentheses but not commentary
    | [_WS] "[" /[^\]]+/  "]" // Often chemical names wrapped in [...]
protein_id : "#" _seperated{NUM_ID, _LIST_SEP} "#"
ref_id     : "<" _seperated{NUM_ID, _LIST_SEP} ">"

TOKEN      : [_WS] (CHAR+ | ARROW | COMPARE_NUM) [_WS]
CHAR       : /\w/
    | /[^\x00-\x7F]/  // Unicode characters
    | "?" | "." | "," | "+" | ":" | "/" | "*" | "-" | "%" | "'"
    | "(H)"  // NADP(H), NAD(H)

ARROW      : "<->" | "<-->" | "<-" | "->" | "-->"
COMPARE_NUM: /p\s?<\s?0\.05/i  // p-values
    | /[<>]/ FLOAT_NUM "%"  // percentages

_COMMENTARY_SEP : /;\s*/
_LIST_SEP  : "," | "\t"
_WS        : ("\t" | " ")+
NUM_ID     : /\d+/
FLOAT_NUM  : /\d+(\.\d+)?/

_seperated{x, sep}: x (sep x)*  // A sequence of 'x sep x sep x ...'
_ACRONYM   : "<acronym>\t"
